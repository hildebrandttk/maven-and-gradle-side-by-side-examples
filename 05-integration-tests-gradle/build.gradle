group = 'tk.hildebrandt.gradle'
version = '1-SNAPSHOT'

subprojects{
   apply plugin: 'java'

   repositories {
      mavenLocal()
      mavenCentral()
   }

   tasks.withType(JavaCompile) {
      options.encoding = 'UTF-8'
   }

   tasks.withType(Javadoc) {
      options.encoding = 'UTF-8'
   }

   tasks.withType(Test) {
      systemProperty "file.encoding", "UTF-8"
      useJUnitPlatform()
   }

   java {
      toolchain {
         languageVersion.set(JavaLanguageVersion.of(17))
      }
   }

   dependencies {
      implementation(enforcedPlatform("org.springframework.boot:spring-boot-dependencies:${springVersion}"))

      constraints {
         implementation 'org.junit.jupiter:junit-jupiter:5.9.1'
      }
   }

   test{
      useJUnitPlatform()

      // EXAMPLE 04-GRADLE-*** Filter unit tests by name
      filter {
         includeTestsMatching "*Test.*"
      }
   }

// EXAMPLE 04-GRADLE-01
   def integrationTestTask = tasks.register('integrationTest', Test) {
      description = 'Runs integration tests.'
      group = 'verification'

      shouldRunAfter(tasks.named('test'))
      // EXAMPLE 04-GRADLE-*** Filter integration tests by name
      filter {
         includeTestsMatching "*IT.*"
      }
      useJUnitPlatform {
         // EXAMPLE 04-GRADLE-02
         excludeTags 'slowtest'
      }
   }

   tasks.named('check') {
      dependsOn(integrationTestTask)
   }

   def slowIntegrationTestTask = tasks.register('slowIntegrationTest', Test) {
      description = 'Runs slow integration tests.'
      group = 'verification'

      shouldRunAfter(tasks.named('test'))
      useJUnitPlatform {
         includeTags 'slowtest'
      }
   }

   // EXAMPLE 04-GRADLE-03
   task withSlowTests (){
      description = 'Runs all tests plus slow tests'
      group = 'verification'
      dependsOn(slowIntegrationTestTask, check)
   }
}