plugins {
   id 'application'
}

test{
   useJUnitPlatform()

   filter {
      includeTestsMatching "*Test.*"
   }
}


def integrationTestTask = tasks.register('integrationTest', Test) {
   description = 'Runs integration tests.'
   group = 'verification'

   shouldRunAfter(tasks.named('test'))
   filter {
      includeTestsMatching "*IT.*"
   }
   useJUnitPlatform {
      excludeTags 'slowtest'
   }
}

def slowIntegrationTestTask = tasks.register('slowIntegrationTest', Test) {
   description = 'Runs slow integration tests.'
   group = 'verification'

   shouldRunAfter(tasks.named('test'))
   filter {
      includeTestsMatching "*IT.*"
   }
   useJUnitPlatform {
      includeTags 'slowtest'
   }
}

def seperatedIntegrationTest = sourceSets.create('seperatedIntegrationTest')

configurations[seperatedIntegrationTest.implementationConfigurationName].extendsFrom(configurations.testImplementation)
configurations[seperatedIntegrationTest.runtimeOnlyConfigurationName].extendsFrom(configurations.testRuntimeOnly)

def seperatedIntegrationTestTask = tasks.register('seperatedIntegrationTest', Test) {
   description = 'Runs integration tests from special directory.'
   group = 'verification'

   testClassesDirs = seperatedIntegrationTest.output.classesDirs
   classpath = configurations[seperatedIntegrationTest.runtimeClasspathConfigurationName] + seperatedIntegrationTest.output

   shouldRunAfter(tasks.named('integrationTest'))
}

tasks.named('check') {
   dependsOn(integrationTestTask)
   dependsOn(slowIntegrationTest)
   dependsOn(seperatedIntegrationTestTask)
}

dependencies {
   module(":lib")
   implementation('org.springframework.boot:spring-boot-starter-data-rest')
   implementation('org.springframework.boot:spring-boot-starter-data-jpa')
   implementation('com.h2database:h2:2.1.214')
   testImplementation('org.springframework.boot:spring-boot-starter-test')
   testImplementation 'org.junit.jupiter:junit-jupiter'
   seperatedIntegrationTestImplementation project
}