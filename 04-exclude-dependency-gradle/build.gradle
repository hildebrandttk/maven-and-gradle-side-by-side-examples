group = "tk.hildebrandt.gradle"
version = "1-SNAPSHOT"

subprojects {
    apply(plugin: "java")

    repositories {
        mavenLocal()
        mavenCentral()
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"
    }

    tasks.withType(Javadoc).configureEach {
        options.encoding = "UTF-8"
    }

    tasks.withType(Test).configureEach {
        systemProperty("file.encoding", "UTF-8")
        useJUnitPlatform()
    }

    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(17))
        }
    }

    dependencies {
        implementation(enforcedPlatform("org.springframework.boot:spring-boot-dependencies:${springVersion}"))

        constraints {
            implementation("org.junit.jupiter:junit-jupiter:5.9.1")
        }
    }
    // EXAMPLE 04-GRADLE-01 Exclude global
    configurations {
        all {
            exclude(group: "org.springframework.boot", module: "spring-boot-starter-logging")
        }
    }
}